legend:
    *       0 or more
    +       1 or more
    ()?     optional
    |       or

statements          : NEWLINE* IDENT* statement (NEWLINE+ statement) NEWLINE*

statement           : import-expr
                    : func-def
                    : KEYWORD:raise expr
                    : KEYWORD:assert expr
                    : expr

# not implemented yet
import-expr         : import IDENTIFIER (COMMA IDENTIFIER (as IDENTIFIER)?)*
                    | from IDENTIFIER import IDENTIFIER (as IDENTIFIER)? (COMMA IDENTIFIER (as IDENTIFIER)?)*

expr                : IDENTIFIER EQ expr
                    : comp-expr ((KEYWORD:and|KEYWORD:or) comp-expr)*
                    : expr IN expr
                    : if-expr
                    : pipe-expr
                    : static-access-expr
                    : range

pattern-match       : IDENTIFIER EQ expr
                    : map-expr EQ expr
                    : list-expr EQ expr
                    : tuple-expr EQ expr

static-access-expr  : expr LSQUARE expr RSQUARE

comp-expr           : KEYWORD:not comp-expr
                    : arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr          : term ((PLUS|MINUS) term)*

term                : factor ((MUL|DIV) factor)*

factor              : (PLUS|MINUS) factor
                    : power

power               : call(POW factor)*

call                : call-expr
                    : atom

call-expr           : atom (
                        LPAREN
                        (expr (COMMA expr)*)?
                        (IDENTIFIER DO expr (COMMA IDENTIFIER EQ expr)*)?
                        RPAREN
                    )?

atom                : INT|FLOAT|IDENTIFIER|STRING|ATOM|PIN IDENTIFIER
                    : LPAREN expr RPAREN
                    : tuple-expr
                    : list-expr
                    : map-expr
                    : lambda-def
                    : case-def
                    : try-expect-expr
                    : func-as-var-expr

list-expr           : LSQUARE (expr ((COMMA expr)|(MUL expr))*)? RSQUARE

map-expr            : LCURLY (((expr DO expr)|(POW expr)) (COMMA ((expr DO expr)|(POW expr)))*)? RCURLY

tuple-expr          : LPAREN expr? COMMA (expr COMMA)* RPAREN

if-expr             : expr KEYWORD:if expr DO expr
                    : (KEYWORD:else expr)?

case-def            : KEYWORD:case expr? DO NEWLINE
                    (expr ARROW (statement|statements) NEWLINE)+

pipe-expr           : expr (PIPE expr)+

func-arg-matchable  : INT|FLOAT|IDENTIFIER|STRING|ATOM|tuple-expr|list-expr|map-expr|IDENTIFIER LARROW func-arg-matchable

func-def            : KEYWORD:def IDENTIFIER
                    LPAREN (func-arg-matchable (COMMA func-arg-matchable)*)? RPAREN
                    (KEYWORD:if expr)?
                    DO
                    (NEWLINE statements)+

lambda-def          : KEYWORD:lambda (func-arg-matchable (COMMA func-arg-matchable)*)?
                    (DO expr)|(NEWLINE statements)+

func-as-var-expr    : ECOM DIV INT


try-expect-expr     : KEYWORD:try DO NEWLINE statements
                    (KEYWORD:except expr? KEYWORD:as IDENTIFIER DO NEWLINE)+
                    (KEYWORD:finally DO NEWLINE statements)?

range               : INT RANGE INT

impl                : KEYWORD:impl IDENTIFIER DO
                    (NEWLINE func-def)+

protocol            : KEYWORD:protocol IDENTIFIER DO
                    (NEWLINE KEYWORD:def LPAREN IDENTIFIER RPAREN)+